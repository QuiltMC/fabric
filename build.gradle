buildscript {
	dependencies {
		classpath 'org.kohsuke:github-api:1.313'
	}
}

plugins {
	id "java-library"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "org.quiltmc.loom" version "1.0.+" apply false
	id "org.quiltmc.gradle.licenser" version "1.+"
	id "net.kyori.indra.git" version "3.0.1"
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.modrinth.minotaur" version "2.4.4"
	id "me.modmuss50.remotesign" version "0.3.0" apply false
	id "io.github.p03w.machete" version "1.2.0"
}

def ENV = System.getenv()
def signingEnabled = ENV.SIGNING_SERVER

version = project.version + "+" + (ENV.GITHUB_RUN_NUMBER ? "" : "local-") + project.upstream_version + "-" + getBranch() + (ENV.SNAPSHOTS_URL ? "-SNAPSHOT" : "")
logger.lifecycle("Building Quilted Fabric API: " + version)


def getQslModule(String library, String module) {
	return "org.quiltmc.qsl.${library}:${module}:${project.qsl_version}";
}

def getSubprojectVersion(project) {
	return version
}

// Same as above, but for QMJ provides and unported modules
def getSubprojectUpstreamVersion(project) {
	def version = project.properties["${project.name}-version"]

	if (!version) {
		throw new NullPointerException("Could not find upstream version for " + project.name)
	}

	return version;
}

def getBranch() {
	def ENV = System.getenv()
	if (ENV.GITHUB_REF) {
		def branch = ENV.GITHUB_REF
		return branch.substring(branch.lastIndexOf("/") + 1)
	}

	if (!indraGit.isPresent()) {
		return "unknown"
	}

	def branch = indraGit.branchName()
	return branch.substring(branch.lastIndexOf("/") + 1)
}

def moduleDependencies(project, List<String> depNames) {
	def deps = depNames.iterator().collect { project.dependencies.project(path: ":$it", configuration: 'namedElements') }
	def clientOutputs = depNames.iterator().collect { findProject(":$it").sourceSets.client.output }

	project.dependencies {
		deps.each {
			api it
		}

		clientOutputs.each {
			clientImplementation it
		}
	}

	// As we manually handle the maven artifacts, we need to also manually specify the deps.
	project.publishing {
		publications {
			mavenJava(MavenPublication) {
				pom.withXml {
					def depsNode = asNode().appendNode("dependencies")
					deps.each {
						def depNode = depsNode.appendNode("dependency")
						depNode.appendNode("groupId", it.group)
						depNode.appendNode("artifactId", it.name)
						depNode.appendNode("version", it.version)
						depNode.appendNode("scope", "compile")
					}
				}
			}
		}
	}
}

def testDependencies(project, List<String> depNames) {
	def deps = depNames.iterator().collect { project.dependencies.project(path: ":$it", configuration: 'namedElements') }
	def clientOutputs = depNames.iterator().collect { findProject(":$it").sourceSets.client.output }

	project.dependencies {
		deps.each {
			testmodImplementation it
		}

		clientOutputs.each {
			testmodImplementation it
		}
	}
}

allprojects {
	group = "org.quiltmc.quilted-fabric-api"

	apply plugin: "maven-publish"
	apply plugin: "me.modmuss50.remotesign"

	tasks.withType(GenerateModuleMetadata) {
		enabled = false
	}

	if (signingEnabled) {
		remoteSign {
			requestUrl = ENV.SIGNING_SERVER
			pgpAuthKey = ENV.SIGNING_PGP_KEY
			jarAuthKey = ENV.SIGNING_JAR_KEY

			afterEvaluate {
				// PGP sign all maven publications.
				sign publishing.publications.mavenJava
			}
		}
	}

	publishing {
		setupRepositories(repositories)
	}

	if (it.name == "deprecated") return

	apply plugin: "java-library"
	apply plugin: "checkstyle"
	apply plugin: "maven-publish"
	apply plugin: "org.quiltmc.loom"
	apply plugin: "org.quiltmc.gradle.licenser"

	tasks.withType(JavaCompile).configureEach {
		it.options.release = 17
	}

	java {
		// Must be added before the split source sets are setup.
		withSourcesJar()
	}

	loom {
		splitEnvironmentSourceSets()
	}

	sourceSets {
		testmod {
			compileClasspath += main.compileClasspath
			runtimeClasspath += main.runtimeClasspath

			compileClasspath += client.compileClasspath
			runtimeClasspath += client.runtimeClasspath
		}
	}

	loom {
		runtimeOnlyLog4j = true

		runs {
			testmodClient {
				client()
				ideConfigGenerated project.rootProject == project
				name = "Testmod Client"
				source sourceSets.testmod
			}
			testmodServer {
				server()
				ideConfigGenerated project.rootProject == project
				name = "Testmod Server"
				source sourceSets.testmod
			}
		}
	}

	allprojects.each { p ->
		if (project.name == "deprecated") return

		loom.mods.register(p.name) {
			sourceSet p.sourceSets.main
			sourceSet p.sourceSets.client
		}

		loom.mods.register(p.name + "-testmod") {
			sourceSet p.sourceSets.testmod
		}
	}

	dependencies {
		minecraft "com.mojang:minecraft:$rootProject.minecraft_version"
		mappings "net.fabricmc:yarn:${rootProject.minecraft_version}${project.yarn_version}:v2"
		modApi "org.quiltmc:quilt-loader:${project.loader_version}"

		testmodImplementation sourceSets.main.output
		testmodImplementation sourceSets.client.output
	}

	repositories {
		mavenLocal()
		maven {
			name = "Quilt"
			url = "https://maven.quiltmc.org/repository/snapshot"
		}
	}

	tasks.withType(ProcessResources).configureEach {
		inputs.property "version", project.version
		inputs.property "upstream_version", project.upstream_version

		filesMatching(["fabric.mod.json", "quilt.mod.json"]) {
			expand "version": project.version, "upstream_version": project.upstream_version
		}
	}

	license {
		rule rootProject.file("codeformat/FABRIC_MODIFIED_HEADER")
		rule rootProject.file("codeformat/HEADER")

		include "**/*.java"
	}

	checkstyle {
		configFile = rootProject.file("codeformat/checkstyle.xml")
		toolVersion = "10.3.3"
	}

	tasks.withType(AbstractArchiveTask) {
		preserveFileTimestamps = false
		reproducibleFileOrder = true
	}

	if (signingEnabled) {
		remoteSign {
			sign remapJar
		}
	}

	// Run this task after updating minecraft to regenerate any required resources
	task generateResources {
		group = "fabric"
	}

	task testmodJar(type: Jar) {
		from sourceSets.testmod.output
		destinationDirectory = new File(project.buildDir, "devlibs")
		archiveClassifier = "testmod"
	}

	[jar, testmodJar, sourcesJar].each {
		it.from(rootProject.file("LICENSE")) {
			rename { "${it}-${project.archivesBaseName}"}
		}
	}

	task remapTestmodJar(type: net.fabricmc.loom.task.RemapJarTask, dependsOn: testmodJar) {
		input = testmodJar.archiveFile
		archiveClassifier = "testmod"
		addNestedDependencies = false
	}
	build.dependsOn remapTestmodJar

	task validateMixinNames(type: net.fabricmc.loom.task.ValidateMixinNameTask) {
		source(sourceSets.main.output)
		source(sourceSets.client.output)
		source(sourceSets.testmod.output)
	}
}

remapTestmodJar {
	def testModJarTasks = []

	subprojects {
		if (it.name == "deprecated" || !it.file("src/testmod").exists()) return

		testModJarTasks += it.tasks.remapTestmodJar
	}

	nestedJars.setFrom(testModJarTasks)
	addNestedDependencies = true
}

// Apply auxiliary buildscripts to submodules
// This must be done after all plugins are applied to subprojects
apply from: "gradle/module-validation.gradle"

javadoc {
	options {
		source = "17"
		encoding = "UTF-8"
		charSet = "UTF-8"
		memberLevel = JavadocMemberLevel.PACKAGE
		links(
				"https://guava.dev/releases/31.0-jre/api/docs/",
				"https://asm.ow2.io/javadoc/",
				"https://docs.oracle.com/en/java/javase/17/docs/api/",
				"https://jenkins.liteloader.com/job/Mixin/javadoc/",
				"https://logging.apache.org/log4j/2.x/log4j-api/apidocs/",
				"https://www.slf4j.org/apidocs/",
				"https://netty.io/4.1/api/",
				"https://javadoc.lwjgl.org/",
				"https://www.javadoc.io/doc/com.google.code.gson/gson/2.9.1/",
				"https://fastutil.di.unimi.it/docs/",
				"https://maven.fabricmc.net/docs/yarn-${rootProject.minecraft_version}${project.yarn_version}/"
		)
		// Disable the crazy super-strict doclint tool in Java 8
		addStringOption("Xdoclint:none", "-quiet")
	}

	allprojects.each {
		if (it.name == "deprecated") return

		source(it.sourceSets.main.allJava.srcDirs)
		source(it.sourceSets.client.allJava.srcDirs)
	}

	classpath = files(sourceSets.main.compileClasspath, sourceSets.client.compileClasspath)
	include("**/api/**")
	failOnError false
}

task javadocJar(type: Jar) {
	dependsOn javadoc
	from javadoc.destinationDir
	//Set as `fatjavadoc` to prevent an ide form trying to use this javadoc, over using the modules javadoc
	archiveClassifier = "fatjavadoc"
}

build.dependsOn javadocJar

loom {
	runs {
		gametest {
			inherit testmodServer

			name "Game Test"

			// Enable the gametest runner
			vmArg "-Dfabric-api.gametest"
			vmArg "-Dfabric-api.gametest.report-file=${project.buildDir}/junit.xml"
			runDir "build/gametest"
		}
		autoTestServer {
			inherit testmodServer

			name "Auto Test Server"

			vmArg "-Dfabric.autoTest"
		}
	}
}
// test.dependsOn runGametest

subprojects {
	if (it.name == "deprecated") return

	dependencies {
		testmodImplementation sourceSets.main.output

		// Make all modules depend on the gametest api (and thus res loader) to try and promote its usage.
		if (project.name != "fabric-gametest-api-v1") {
			testmodImplementation project(path: ':fabric-gametest-api-v1', configuration: 'namedElements')
			testmodImplementation project(path: ':fabric-resource-loader-v0', configuration: 'namedElements')
		}

		// Make all testmods run with registry-sync-v0 as it is required to register new objects.
		if (project.name != "fabric-registry-sync-v0") {
			testmodRuntimeOnly project(path: ':fabric-registry-sync-v0', configuration: 'namedElements')
		}
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				artifact(signingEnabled ? signRemapJar.output : remapJar) {
					builtBy(signingEnabled ? signRemapJar : remapJar)
				}

				artifact(remapSourcesJar) {
					builtBy remapSourcesJar
				}
			}
		}
	}

	// We manually handle the pom generation
	loom.disableDeprecatedPomGeneration(publishing.publications.mavenJava)

	javadoc.enabled = false

	afterEvaluate {
		// Disable the gen sources task on sub projects
		genClientOnlySourcesWithQuiltflower.enabled = false
		genClientOnlySourcesWithFernFlower.enabled = false
		genClientOnlySourcesWithCfr.enabled = false
		genCommonSourcesWithQuiltflower.enabled = false
		genCommonSourcesWithFernFlower.enabled = false
		genCommonSourcesWithCfr.enabled = false
		unpickClientOnlyJar.enabled = false
		unpickCommonJar.enabled = false
	}
}

task remapMavenJar(type: net.fabricmc.loom.task.RemapJarTask, dependsOn: jar) {
	input = jar.archiveFile
	archiveFileName = "${archivesBaseName}-${project.version}-maven.jar"
	addNestedDependencies = false
}
build.dependsOn remapMavenJar

if (signingEnabled) {
	remoteSign {
		sign remapMavenJar
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(signingEnabled ? signRemapMavenJar.output : remapMavenJar) {
				builtBy(signingEnabled ? signRemapMavenJar : remapMavenJar)
			}

			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}

			artifact javadocJar

			pom.withXml {
				def depsNode = asNode().appendNode("dependencies")
				getAllQslModules().forEach {
					def depNode = depsNode.appendNode("dependency")
					depNode.appendNode("groupId", it.getModuleGroup())
					depNode.appendNode("artifactId", it.getModuleName())
					depNode.appendNode("version", it.getModuleVersion())
					depNode.appendNode("scope", "compile")
				}
				subprojects.each {
					// Dont depend on the deprecated modules in the main artifact.
					if (it.path.startsWith(":deprecated")) return

					def depNode = depsNode.appendNode("dependency")
					depNode.appendNode("groupId", it.group)
					depNode.appendNode("artifactId", it.name)
					depNode.appendNode("version", it.version)
					depNode.appendNode("scope", "compile")
				}
			}
		}
	}
}

// Required until the deprecation is removed. Fabric API's main jar that is published to maven does not contain sub modules.
loom.disableDeprecatedPomGeneration(publishing.publications.mavenJava)

void setupRepositories(RepositoryHandler repositories) {
	//repositories.mavenLocal() // uncomment for testing
	def ENV = System.getenv()
	if (ENV.MAVEN_URL) {
		repositories.maven {
			url ENV.MAVEN_URL
			if (ENV.MAVEN_USERNAME) {
				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
			}
		}
	} else if (ENV.SNAPSHOTS_URL) {

		repositories.maven {
			url ENV.SNAPSHOTS_URL
			credentials {
				username ENV.SNAPSHOTS_USERNAME
				password ENV.SNAPSHOTS_PASSWORD
			}
		}
	} else {
		repositories.mavenLocal()
	}
}

subprojects.each {
	if (it.name == "deprecated") return

	remapJar.dependsOn("${it.path}:remapJar")
}

sourceSets {
	testmod
}

// These modules are not included in the fat jar, maven will resolve them via the pom.
def devOnlyModules = [
		"fabric-gametest-api-v1",
]

configurations {
	qsl
}
dependencies {
	// Bundle all QSL modules
	qsl modApi("org.quiltmc:qsl:${project.qsl_version}")
	getAllQslModules().forEach {
		include (it.getModuleGroup() + ":" + it.getModuleName() + ":" + it.getModuleVersion())
	}
	afterEvaluate {
		subprojects.each {
			if (it.name == "deprecated") return

			api project(path: "${it.path}", configuration: "namedElements")
			clientImplementation project("${it.path}:").sourceSets.client.output

			testmodImplementation project("${it.path}:").sourceSets.testmod.output
		}
	}
}

java.util.stream.Stream<ResolvedDependency> getAllQslModules() {
// "it" is QSL
	return configurations.getByName("qsl").resolvedConfiguration.getFirstLevelModuleDependencies().stream().flatMap {
		it.getChildren().stream()
	}.flatMap {
		// "it" is each QSL library
		it.getChildren().stream()
	}.filter {
		// "it" is each QSL module
		// this should only be QSL modules, but it can't hurt to filter out
		if (it.getModuleGroup().startsWith("org.quiltmc.qsl") && it.getModuleGroup().split("\\.").length == 4) {
			return true
		} else {
			throw RuntimeException("Unexpected dependency: $it")
		}
	}

}
remapJar {
	afterEvaluate {
		subprojects.each {
			if (it.name in devOnlyModules || it.name == "deprecated") return

			// Include the signed or none signed jar from the sub project.
			nestedJars.from project("${it.path}").tasks.getByName(signingEnabled ? "signRemapJar" : "remapJar")
		}
	}
}

machete {
	// JSON minification isn't effective on this scale and sacrifices readability
	optimizations.json = false
}
// This prevents issues with execution optimizations and therefore, Gradle deprecation warnings
prepareRemapJar.dependsOn("optimizeOutputsOfJar")
prepareRemapMavenJar.dependsOn("optimizeOutputsOfJar")

task renameJar(type: Jar, dependsOn: (signingEnabled ? signRemapJar : remapJar)) {
	with remapJar
	from zipTree(remapJar.archiveFile)
	def qsl_version_normalized = project.qsl_version.substring(0, project.qsl_version.indexOf('+'))
	def qfapi_version_normalized = project.version.substring(0, project.version.indexOf('+'))
	def upstream_fapi_version_normalized = project.upstream_version
	archiveFileName.set("qfapi-${qfapi_version_normalized}_qsl-${qsl_version_normalized}_fapi-${upstream_fapi_version_normalized}_mc-${project.minecraft_version}.jar")
}

curseforge {
	if (ENV.CURSEFORGE_TOKEN) {
		apiKey = ENV.CURSEFORGE_TOKEN
	}

	project {
		id = "634179"
		changelog = ENV.CHANGELOG ?: "No changelog provided"
		releaseType = project.prerelease == "true" ? "beta" : "release"
		addGameVersion "1.19.2"
		addGameVersion "Quilt"

		mainArtifact(renameJar) {
			displayName = "[$project.minecraft_version] QFAPI " + project.version.split("\\+")[0] + " and QSL " + project.qsl_version.split("\\+")[0]
		}

		afterEvaluate {
			uploadTask.dependsOn("remapJar")
		}
	}

	options {
		forgeGradleIntegration = false
	}
}
tasks.curseforge.dependsOn renameJar

if (signingEnabled) {
	project.tasks.curseforge.dependsOn signRemapJar
	project.tasks.modrinth.dependsOn signRemapJar
	build.dependsOn signRemapJar
}

import org.kohsuke.github.GHReleaseBuilder
import org.kohsuke.github.GitHub


task github(dependsOn: renameJar) {
	onlyIf {
		ENV.GITHUB_TOKEN
	}

	doLast {
		def github = GitHub.connectUsingOAuth(ENV.GITHUB_TOKEN as String)
		def repository = github.getRepository(ENV.GITHUB_REPOSITORY)

		def releaseBuilder = new GHReleaseBuilder(repository, version as String)
		releaseBuilder.name("[$project.minecraft_version] QFAPI " + project.version.split("\\+")[0] + " and QSL " + project.qsl_version.split("\\+")[0])
		releaseBuilder.body("Provides Fabric API ${project.upstream_version}\n" + ENV.CHANGELOG ?: "No changelog provided")
		releaseBuilder.commitish(getBranch())
		releaseBuilder.prerelease(project.prerelease == "true")

		def ghRelease = releaseBuilder.create()
		ghRelease.uploadAsset(renameJar.archiveFile.get().getAsFile(), "application/java-archive");
	}
}

modrinth {
	projectId = "qsl"
	versionName = "[$project.minecraft_version] QFAPI " + project.version.split("\\+")[0] + " and QSL " + project.qsl_version.split("\\+")[0]
	versionType = project.prerelease == "true" ? "beta" : "release"
	changelog = "Provides Fabric API ${project.upstream_version}\n" + (ENV.CHANGELOG ?: "No changelog provided")
	dependencies {
		embedded.project "fabric-api"
	}

	uploadFile = signingEnabled ? signRemapJar.output : remapJar
}

tasks.modrinth.dependsOn renameJar
